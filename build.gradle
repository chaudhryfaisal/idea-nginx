import org.ajoberstar.grgit.Grgit

plugins {
    id "org.jetbrains.intellij" version "0.2.17"
    id "org.ajoberstar.grgit" version "2.0.0"
    id "java"
    id "idea"
}

group 'net.ishchenko.idea.nginx'
version '0.1.5-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

sourceSets {
    main {
        java.srcDirs 'src', 'gen'
        resources.srcDir 'resources'
    }
    test {
        java.srcDir 'tests'
    }
}

intellij {
    version '2017.1' //IntelliJ IDEA dependency
    //updateSinceUntilBuild false
    //plugins 'coverage' //Bundled plugin dependencies
    pluginName 'idea-nginx-support'

    publishPlugin {
        username user
        password pass
    }
}

task generateDocumentation(type: JavaExec) {
    main = 'net.ishchenko.idea.nginx.KeywordsFromSourcesGenerator'
    classpath = sourceSets.main.runtimeClasspath
}

task getRepos() {
    doLast {
        def path = 'vendor/nginx'
        if (!file(path).exists()) {
            Grgit.clone(dir: file(path), uri: 'https://github.com/nginx/nginx.git')
        } else {
            Grgit.open(dir: file(path)).pull()
        }
        def luaPath = 'vendor/lua-nginx-module'
        if (!file(luaPath).exists()) {
            Grgit.clone(dir: file(luaPath), uri: 'https://github.com/openresty/lua-nginx-module.git')
        } else {
            Grgit.open(dir: file(luaPath)).pull()
        }
    }
}
